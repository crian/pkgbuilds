# Maintainer: crian <crian84@gmail.com>

pkgname=tabbed-crian-git
pkgver=r259.94ce87d
pkgrel=1
pkgdesc='Simple generic tabbed fronted to xembed aware applications'
arch=('x86_64')
url="https://github.com/crian/suckless"
license=('MIT')
depends=('libx11')
conflicts=('tabbed')
provides=('tabbed')
source=("git+${url}.git#branch=tabbed")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/suckless"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/suckless"
    echo 'Copying config.h from $startdir if it exists...'
    [ -f "${startdir}/config.h" ] && cp "${startdir}/config.h" . || true
}

package() {
    cd "$srcdir/suckless"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/tabbed/LICENSE"
    install -m644 -D README "$pkgdir/usr/share/doc/tabbed/README"
}
